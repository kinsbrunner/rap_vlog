@Metadata.layer: #CORE
@UI: { headerInfo: { typeName: 'Request',
                     typeNamePlural: 'Requests',
                     title: { type: #STANDARD,
                              value: 'ExternalId' },
                     description.value: 'StatusText' } }

annotate entity ZC_Request with
{
  @UI.facet: [ { id: 'Request',
                 purpose: #STANDARD,
                 type: #IDENTIFICATION_REFERENCE,
                 label: 'Request',
                 position: 10 },

               { id: 'RequestItems',
                 purpose: #STANDARD,
                 type: #LINEITEM_REFERENCE,
                 label: 'Requested Products',
                 position: 20,
                 targetElement: 'items' } ]

  @UI.lineItem: [{ type: #FOR_ACTION,
                   label: 'Cancel Request',
                   dataAction: 'cancel_request',
                   requiresContext: true,
                   invocationGrouping: #CHANGE_SET }] //This makes the call a single time when doing in batch

  @UI.identification: [{ type: #FOR_ACTION,
                         position: 10, 
                         label: 'Add Quick Product',
                         dataAction: 'add_item' }]


  @UI.hidden: true
  RequestUuid;

  @EndUserText.label: 'External ID'
  @UI: { lineItem: [{ position: 10,
                      label: 'External ID' }] }
  ExternalId;

  @UI: { selectionField: [{ position: 20 }],
         textArrangement: #TEXT_ONLY,
         lineItem: [{ position: 20,
                      label: 'Requester' }],
         identification: [{ position: 10,
                            label: 'Requester' }] }
  RequesterId;

  @UI.hidden: true
  Status;

  @UI: { selectionField: [{ position: 30 }],
         textArrangement: #TEXT_ONLY,
         lineItem: [{ position: 30,
                      label: 'Status',
                      criticality: 'StatusCriticality',
                      criticalityRepresentation: #WITH_ICON }] }
  StatusFilter;

  @UI.hidden: true
  StatusCriticality;

  @UI: { selectionField: [{ position: 40 }],
         textArrangement: #TEXT_ONLY,
         lineItem: [{ position: 40,
                      label: 'Priority' }],
         identification: [{ position: 30 }] }
  Priority;

  @UI: { lineItem: [{ position: 50,
                      label: 'Deadline Date' }],
         identification: [{ position: 40,
                            label: 'Deadline Date' }] }
  DeadlineDate;

  @UI: { lineItem: [{ position: 80,
                      label: 'Cancellation Reason' }],
         identification: [{ position: 80,
                            label: 'Cancellation Reason' }] }
  @UI.multiLineText: true
  @UI.hidden: #( CancelReasonHidden )
  CancelReason;

  @UI.lineItem: [{ position: 70,
                   label: 'Last Changed At' }]
  LastChangedAt;
}