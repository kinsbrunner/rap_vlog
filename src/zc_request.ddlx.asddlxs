@Metadata.layer: #CORE
@UI: { headerInfo: { typeName: 'Request',
                     typeNamePlural: 'Requests',
                     title: { type: #STANDARD,
                              value: 'ExternalId' },
                     description.value: 'StatusText' } }

annotate entity ZC_Request with
{
  @UI.facet: [ { id: 'Request',
                 purpose: #STANDARD,
                 type: #IDENTIFICATION_REFERENCE,
                 label: 'Request',
                 position: 10,
                 targetQualifier : 'Main' },

               { id: 'ReqCancel',
                 purpose: #STANDARD,
                 type: #FIELDGROUP_REFERENCE,
                 label: 'Cancellation Reason',
                 position: 15,
                 targetQualifier : 'Cancel'
               },

               { id: 'RequestItems',
                 purpose: #STANDARD,
                 type: #LINEITEM_REFERENCE,
                 label: 'Requested Products',
                 position: 20,
                 targetElement: 'items' } ]

  @UI.lineItem: [{ type: #FOR_ACTION,
                   label: 'Cancel Request',
                   dataAction: 'cancel_request',
                   requiresContext: true,
                   invocationGrouping: #CHANGE_SET }] //This makes the call a single time when doing in batch


  @UI.hidden: true
  RequestUuid;

  @EndUserText.label: 'External ID'
  @UI: { lineItem: [{ position: 10,
                      label: 'External ID' }] }
  ExternalId;

  @UI: { selectionField: [{ position: 20 }],
         textArrangement: #TEXT_ONLY,
         lineItem: [{ position: 20,
                      label: 'Requester' }],
         identification: [{ qualifier: 'Main',
                            position: 10,
                            label: 'Requester' }] }
  RequesterId;

  @UI: { identification: [{ qualifier: 'Main',
                            position: 20,
                            criticality: 'StatusCriticality',
                            criticalityRepresentation: #WITH_ICON }] }
  Status;

  @UI: { selectionField: [{ position: 30 }],
         textArrangement: #TEXT_ONLY,
         lineItem: [{ position: 30,
                      label: 'Status',
                      criticality: 'StatusCriticality',
                      criticalityRepresentation: #WITH_ICON }] }
  StatusFilter;

  @UI.hidden: true
  StatusCriticality;

  @UI: { selectionField: [{ position: 40 }],
         textArrangement: #TEXT_ONLY,
         lineItem: [{ position: 40,
                      label: 'Priority' }],
         identification: [{ qualifier: 'Main',
                            position: 30 }] }
  Priority;

  @UI: { lineItem: [{ position: 50,
                      label: 'Deadline Date' }],
         identification: [{ qualifier: 'Main',
                            position: 40,
                            label: 'Deadline Date' }] }
  DeadlineDate;

  @UI: { lineItem: [{ position: 80,
                      label: 'Cancellation Reason' }],
         fieldGroup: [{ qualifier: 'Cancel',
                            position: 10,
                            label: '' }] }
  @UI.multiLineText: true
  CancelReason;

  @UI.lineItem: [{ position: 70,
                   label: 'Last Changed At' }]
  LastChangedAt;
}